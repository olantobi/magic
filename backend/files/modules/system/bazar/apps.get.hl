
/*
 * Returns Bazar apps that the local server can install.
 */
.description:Returns Bazar apps that the local server can install


/*
 * Sanity checking invocation.
 */
auth.ticket.verify:root


/*
 * Retrieving configured Bazars.
 */
config.get:"magic:bazars"


/*
 * Splitting above on commas, to allow for declaring multiple bazars
 * in the same config key.
 */
strings.split:x:@config.get
   .:,


/*
 * Iterating through each bazar, retrieving manifest file,
 * and returning contents to caller as JSON.
 */
for-each:x:@strings.split/*

   /*
    * Retrieving manifest.
    */
   http.get:x:@.dp/#
   if
      not
         eq
            get-value:x:@http.get
            .:int:200
      .lambda

         /*
          * Oops ...
          */
         throw:Bazar manifest is unreachable for some reasons
            status:int:500
            public:bool:true

   /*
    * Converting Bazar to relevant model and returning all apps in it to caller.
    */
   hyper2lambda:x:@http.get/*/content
   add:x:../*/return
      get-nodes:x:@hyper2lambda/*

/*
 * Returning all apps to caller making sure we apply some aggressive caching
 * in the process.
 */
response.headers.add
   Cache-Control:public, max-age=3600
return
